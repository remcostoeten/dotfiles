# Add dotfiles bin to PATH and make executables available
if test -d ~/.config/dotfiles/bin
    contains ~/.config/dotfiles/bin $PATH; or set -gx PATH ~/.config/dotfiles/bin $PATH
    
    # Ensure all files are executable
    for file in ~/.config/dotfiles/bin/*
        if test -f $file
            chmod +x $file
        end
    end
end

# Source aliases
if test -d ~/.config/dotfiles/aliases
    for alias_file in ~/.config/dotfiles/aliases/*.fish
        if test -f $alias_file
            source $alias_file
        end
    end
end

# Source fish functions
if test -d ~/.config/dotfiles/fish/functions
    # Add functions directory to fish function path
    contains ~/.config/dotfiles/fish/functions $fish_function_path; or set -gx fish_function_path ~/.config/dotfiles/fish/functions $fish_function_path
end

# Source fish conf.d files
if test -d ~/.config/dotfiles/fish/conf.d
    for conf_file in ~/.config/dotfiles/fish/conf.d/*.fish
        if test -f $conf_file
            source $conf_file
        end
    end
end

# Add scripts directory to PATH
if test -d ~/.config/dotfiles/scripts
    contains ~/.config/dotfiles/scripts $PATH; or set -gx PATH ~/.config/dotfiles/scripts $PATH
    
    # Ensure all files are executable
    for file in ~/.config/dotfiles/scripts/*
        if test -f $file
            chmod +x $file
        end
    end
end

function fish_prompt -d "Write out the prompt"
    # This shows up as USER@HOST /home/user/ >, with the directory colored
    # $USER and $hostname are set by fish, so you can just use them
    # instead of using `whoami` and `hostname`
    printf '%s@%s %s%s%s > ' $USER $hostname \
        (set_color $fish_color_cwd) (prompt_pwd) (set_color normal)
end

if status is-interactive # Commands to run in interactive sessions can go here
    # Editor
    set -gx EDITOR nvim
    set -gx SUDO_EDITOR $EDITOR

    # bun
    set -gx BUN_INSTALL $HOME/.bun
    fish_add_path $BUN_INSTALL/bin

    # Load secrets
    if test -f $HOME/.secrets
        source $HOME/.secrets
    end

    # No greeting
    set fish_greeting

    # Use starship
    starship init fish | source
    if test -f ~/.local/state/quickshell/user/generated/terminal/sequences.txt
        cat ~/.local/state/quickshell/user/generated/terminal/sequences.txt
    end

    # Aliases
    alias pamcan pacman
    alias ls 'eza --icons'
    alias clear "printf '\033[2J\033[3J\033[1;1H'"
    alias q 'qs -c ii'
    alias c clear
    alias dot 'cd ~/.config/dotfiles'
    alias x exit
    alias rm 'rm -rf'
    alias p pnpm
    alias r 'bun run dev'
    alias b 'bun run build'
    alias i 'bun install'
    alias t 'bun run tauri dev'

    # Cargo
    if test -f $HOME/.cargo/env
        bass source $HOME/.cargo/env
    end

    # Local bin env
    if test -f $HOME/.local/bin/env
        bass source $HOME/.local/bin/env
    end
end
